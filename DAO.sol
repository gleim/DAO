/*
This file is part of the DAO.

The DAO is free software: you can redistribute it and/or modify
it under the terms of the GNU lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

The DAO is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU lesser General Public License for more details.

You should have received a copy of the GNU lesser General Public License
along with the DAO.  If not, see <http://www.gnu.org/licenses/>.
*/


/*
Standard smart contract for a Decentralized Autonomous Organization (DAO)
to automate organizational governance and decision-making.
*/

import "./TokenCreation.sol";
import "./ManagedAccount.sol";

contract DAOInterface {

    // The amount of days for which people who try to participate in the
    // creation by calling the fallback function will still get their ether back
    uint constant creationGracePeriod = 40 days;
    // The minimum debate period that a generic proposal can have
    uint constant minProposalDebatePeriod = 2 weeks;
    // The minimum debate period that a split proposal can have
    uint constant minSplitDebatePeriod = 1 weeks;
    // Period of days inside which it's possible to execute a DAO split
    uint constant splitExecutionPeriod = 27 days;
    // Period of time after which the minimum Quorum is halved
    uint constant quorumHalvingPeriod = 25 weeks;
    // Period after which a proposal is closed
    // (used in the case `executeProposal` fails because it throws)
    uint constant executeProposalPeriod = 10 days;
    // Denotes the maximum proposal deposit that can be given. It is given as
    // a fraction of total Ether spent plus balance of the DAO
    uint constant maxDepositDivisor = 100;

    // Proposals to spend the DAO's ether or to choose a new Curator
    Proposal[] public proposals;
    // The quorum needed for each proposal is partially calculated by
    // totalSupply / minQuorumDivisor
    uint public minQuorumDivisor;
    // The unix time of the last time quorum was reached on a proposal
    uint  public lastTimeMinQuorumMet;

    // Address of the curator
    address public curator;
    // The whitelist: List of addresses the DAO is allowed to send ether to
    mapping (address => bool) public allowedRecipients;

    // Tracks the addresses that own Reward Tokens. Those addresses can only be
    // DAOs that have split from the original DAO. Conceptually, Reward Tokens
    // represent the proportion of the rewards that the DAO has the right to
    // receive. These Reward Tokens are generated when the DAO spends ether.
    mapping (address => uint) public rewardToken;
    // Total supply of rewardToken
    uint public totalRewardToken;

    // The account used to manage the rewards which are to be distributed to the
    // DAO Token Holders of this DAO
    ManagedAccount public rewardAccount;

    // The account used to manage the rewards which are to be distributed to
    // any DAO that holds Reward Tokens
    ManagedAccount public DAOrewardAccount;

    // Amount of rewards (in wei) already paid out to a certain DAO
    mapping (address => uint) public DAOpaidOut;

    // Amount of rewards (in wei) already paid out to a certain address
    mapping (address => uint) public paidOut;
    // Map of addresses blocked during a vote (not allowed to transfer DAO
    // tokens). The address points to the proposal ID.
    mapping (address => uint) public blocked;

    // The minimum deposit (in wei) required to submit any proposal that is not
    // requesting a new Curator (no deposit is required for splits)
    uint public proposalDeposit;

    // the accumulated sum of all current proposal deposits
    uint sumOfProposalDeposits;

    // Contract that is able to create a new DAO (with the same code as
    // this one), used for splits
    DAO_Creator public daoCreator;



    // Default delegate
    // All tokenHolders that has not defined a delegate defaults to this delegate (delegate 0)
    address defaultDelegate;

    // Who is the delegate for each token Holders
    // Zero means the default delegate
    mapping (address => VotesAssignedToTokenHolder[]) votesAssignedToTokenHolders;

    // Each delegate has an array of delegations
    // When a new delegation change, it will take effect in the next proposal Id
    // This will be noted at the end of the array adding a Delegation if necessary
    mapping (address => VotesAssignedToDelegate[]) votesAssignedToDelegates;


    // A proposal with `newCurator == false` represents a transaction
    // to be issued by this DAO
    // A proposal with `newCurator == true` represents a DAO split
    struct Proposal {
        // The address where the `amount` will go to if the proposal is accepted
        // or if `newCurator` is true, the proposed Curator of
        // the new DAO).
        address recipient;
        // The amount to transfer to `recipient` if the proposal is accepted.
        uint amount;
        // A plain text description of the proposal
        string description;
        // A unix timestamp, denoting the end of the voting period
        uint votingDeadline;
        // True if the proposal's votes have yet to be counted, otherwise False
        bool open;
        // True if quorum has been reached, the votes have been counted, and
        // the majority said yes
        bool proposalPassed;
        // A hash to check validity of a proposal
        bytes32 proposalHash;
        // Deposit in wei the creator added when submitting their proposal. It
        // is taken from the msg.value of a newProposal call.
        uint proposalDeposit;
        // True if this proposal is to assign a new Curator
        bool newCurator;
        // Data needed for splitting the DAO
        SplitData[] splitData;
        // Number of Tokens in favor of the proposal
        uint yea;
        // Number of Tokens opposed to the proposal
        uint nay;
        // Simple mapping to check if a shareholder has voted for it
        mapping (address => bool) votedYes;
        // Simple mapping to check if a shareholder has voted against it
        mapping (address => bool) votedNo;
        // Bypassed votes for each delegate
        mapping (address => uint) bypassedDelegatedVotes;
        // Address of the shareholder who created the proposal
        address creator;
    }

    // Used only in the case of a newCurator proposal.
    struct SplitData {
        // The balance of the current DAO minus the deposit at the time of split
        uint splitBalance;
        // The total amount of DAO Tokens in existence at the time of split.
        uint totalSupply;
        // Amount of Reward Tokens owned by the DAO at the time of split.
        uint rewardToken;
        // The new DAO contract created at the time of split.
        DAO newDAO;
    }


    struct VotesAssignedToDelegate {
        // proposal when starts to take effect the delegation
        uint fromProposalId;
        // Number of votes has been delegated to this delegate.
        uint delegatedVotes;
    }

    struct VotesAssignedToTokenHolder {
        // proposal when starts to take effect the delegation
        uint fromProposalId;
        // nomber of votes assigned to token holder from proposal
        uint votes;
        // Address of the delegate assigned
        address delegate;
    }

    // Used to restrict access to certain functions to only DAO Token Holders
    modifier onlyTokenholders {}

    /// @dev Constructor setting the Curator and the address
    /// for the contract able to create another DAO as well as the parameters
    /// for the DAO Token Creation
    /// @param _curator The Curator
    /// @param _defaultDelegate Default delegate of the DAO
    /// @param _daoCreator The contract able to (re)create this DAO
    /// @param _proposalDeposit The deposit to be paid for a regular proposal
    /// @param _minTokensToCreate Minimum required wei-equivalent tokens
    ///        to be created for a successful DAO Token Creation
    /// @param _closingTime Date (in Unix time) of the end of the DAO Token Creation
    /// @param _privateCreation If zero the DAO Token Creation is open to public, a
    /// non-zero address means that the DAO Token Creation is only for the address
    // This is the constructor: it can not be overloaded so it is commented out
    //  function DAO(
        //  address _curator,
        //  address _defaultDelegate
        //  DAO_Creator _daoCreator,
        //  uint _proposalDeposit,
        //  uint _minTokensToCreate,
        //  uint _closingTime,
        //  address _privateCreation
    //  );

    /// @notice Create Token with `msg.sender` as the beneficiary
    /// @return Whether the token creation was successful
    function () returns (bool success);


    /// @dev This function is used to send ether back
    /// to the DAO, it can also be used to receive payments that should not be
    /// counted as rewards (donations, grants, etc.)
    /// @return Whether the DAO received the ether successfully
    function receiveEther() returns(bool);

    /// @notice `msg.sender` creates a proposal to send `_amount` Wei to
    /// `_recipient` with the transaction data `_transactionData`. If
    /// `_newCurator` is true, then this is a proposal that splits the
    /// DAO and sets `_recipient` as the new DAO's Curator.
    /// @param _recipient Address of the recipient of the proposed transaction
    /// @param _amount Amount of wei to be sent with the proposed transaction
    /// @param _description String describing the proposal
    /// @param _transactionData Data of the proposed transaction
    /// @param _debatingPeriod Time used for debating a proposal, at least 2
    /// weeks for a regular proposal, 10 days for new Curator proposal
    /// @param _newCurator Bool defining whether this proposal is about
    /// a new Curator or not
    /// @return The proposal ID. Needed for voting on the proposal
    function newProposal(
        address _recipient,
        uint _amount,
        string _description,
        bytes _transactionData,
        uint _debatingPeriod,
        bool _newCurator
    ) onlyTokenholders returns (uint _proposalID);

    /// @notice Check that the proposal with the ID `_proposalID` matches the
    /// transaction which sends `_amount` with data `_transactionData`
    /// to `_recipient`
    /// @param _proposalID The proposal ID
    /// @param _recipient The recipient of the proposed transaction
    /// @param _amount The amount of wei to be sent in the proposed transaction
    /// @param _transactionData The data of the proposed transaction
    /// @return Whether the proposal ID matches the transaction data or not
    function checkProposalCode(
        uint _proposalID,
        address _recipient,
        uint _amount,
        bytes _transactionData
    ) constant returns (bool _codeChecksOut);

    /// @notice Vote on proposal `_proposalID` with `_supportsProposal`
    /// @param _proposalID The proposal ID
    /// @param _supportsProposal Yes/No - support of the proposal
    /// @return The vote ID.
    function vote(
        uint _proposalID,
        bool _supportsProposal
    ) onlyTokenholders returns (uint _voteID);

    /// @notice Checks whether proposal `_proposalID` with transaction data
    /// `_transactionData` has been voted for or rejected, and executes the
    /// transaction in the case it has been voted for.
    /// @param _proposalID The proposal ID
    /// @param _transactionData The data of the proposed transaction
    /// @return Whether the proposed transaction has been executed or not
    function executeProposal(
        uint _proposalID,
        bytes _transactionData
    ) returns (bool _success);

    /// @notice ATTENTION! I confirm to move my remaining ether to a new DAO
    /// with `_newCurator` as the new Curator, as has been
    /// proposed in proposal `_proposalID`. This will burn my tokens. This can
    /// not be undone and will split the DAO into two DAO's, with two
    /// different underlying tokens.
    /// @param _proposalID The proposal ID
    /// @param _newCurator The new Curator of the new DAO
    /// @dev This function, when called for the first time for this proposal,
    /// will create a new DAO and send the sender's portion of the remaining
    /// ether and Reward Tokens to the new DAO. It will also burn the DAO Tokens
    /// of the sender.
    function splitDAO(
        uint _proposalID,
        address _newCurator
    ) returns (bool _success);

    /// @dev can only be called by the DAO itself through a proposal
    /// updates the contract of the DAO by sending all ether and rewardTokens
    /// to the new DAO. The new DAO needs to be approved by the Curator
    /// @param _newContract the address of the new contract
    function newContract(address _newContract);


    /// @notice Add a new possible recipient `_recipient` to the whitelist so
    /// that the DAO can send transactions to them (using proposals)
    /// @param _recipient New recipient address
    /// @dev Can only be called by the current Curator
    /// @return Whether successful or not
    function changeAllowedRecipients(address _recipient, bool _allowed) external returns (bool _success);


    /// @notice Change the minimum deposit required to submit a proposal
    /// @param _proposalDeposit The new proposal deposit
    /// @dev Can only be called by this DAO (through proposals with the
    /// recipient being this DAO itself)
    function changeProposalDeposit(uint _proposalDeposit) external;

    /// @notice Move rewards from the DAORewards managed account
    /// @param _toMembers If true rewards are move to the actual reward account
    ///                   for the DAO. If not then it's moved to the DAO itself
    /// @return Whether the call was successful
    function retrieveDAOReward(bool _toMembers) external returns (bool _success);

    /// @notice Get my portion of the reward that was sent to `rewardAccount`
    /// @return Whether the call was successful
    function getMyReward() returns(bool _success);

    /// @notice Withdraw `_account`'s portion of the reward from `rewardAccount`
    /// to `_account`'s balance
    /// @return Whether the call was successful
    function withdrawRewardFor(address _account) internal returns (bool _success);

    /// @notice Send `_amount` tokens to `_to` from `msg.sender`. Prior to this
    /// getMyReward() is called.
    /// @param _to The address of the recipient
    /// @param _amount The amount of tokens to be transfered
    /// @return Whether the transfer was successful or not
    function transferWithoutReward(address _to, uint256 _amount) returns (bool success);

    /// @notice Send `_amount` tokens to `_to` from `_from` on the condition it
    /// is approved by `_from`. Prior to this getMyReward() is called.
    /// @param _from The address of the sender
    /// @param _to The address of the recipient
    /// @param _amount The amount of tokens to be transfered
    /// @return Whether the transfer was successful or not
    function transferFromWithoutReward(
        address _from,
        address _to,
        uint256 _amount
    ) returns (bool success);

    /// @notice Doubles the 'minQuorumDivisor' in the case quorum has not been
    /// achieved in 52 weeks
    /// @return Whether the change was successful or not
    function halveMinQuorum() returns (bool _success);

    /// @return total number of proposals ever created
    function numberOfProposals() constant returns (uint _numberOfProposals);

    /// @param _proposalID Id of the new curator proposal
    /// @return Address of the new DAO
    function getNewDAOAdress(uint _proposalID) constant returns (address _newDAO);


    /// @param _account The address of the account which is checked.
    /// @return Whether the account is blocked (not allowed to transfer tokens) or not.
    function isBlocked(address _account) returns (bool);

    /// @param _delegate The address of the new delegate representhing calling token holder
    /// @return Whether the change was successful or not
    function setDelegate(address _delegate) returns (bool _success);

    /// @notice Return address of the delegate representing _tokenHolder for a specifip propolal.
    /// @param _tokenHolder The tokenHolder to determine the delegate
    /// @param _proposalID The proposal ID for which you want to determine the tokenHolder delegate's address
    /// @return votes The number of votes that can use in this proposal
    /// @return delegate The tokenHolder deletate's address of this proposal.
    function getTokenHolderVotingRights(address _tokenHolder, uint _proposalID) constant returns (uint votes, address delegate);

    /// @notice Return number of votes controlled by a delegate for a specific proposal
    /// @param _delegate The delegate address for which wou want to deretmine the controlled votes
    /// @param _proposalID The proposal ID for which you want to determine the votes controlled by the delegate
    /// @return The number of votes that _delegate controls.
    function getDelegateVotingRights(address _delegate, uint _proposalID) constant returns (uint votes);




    event ProposalAdded(
        uint indexed proposalID,
        address recipient,
        uint amount,
        bool newCurator,
        string description
    );
    event Voted(uint indexed proposalID, bool position, address indexed voter);
    event ProposalTallied(uint indexed proposalID, bool result, uint quorum);
    event NewCurator(address indexed _newCurator);
    event NewDefaultDelegate(address indexed _newDefaultDelegate);
    event AllowedRecipientChanged(address indexed _recipient, bool _allowed);
}

// The DAO contract itself
contract DAO is DAOInterface, Token, TokenCreation {

    // Modifier that allows only shareholders to vote and create new proposals
    modifier onlyTokenholders {
        if (balanceOf(msg.sender) == 0) throw;
            _
    }

    function DAO(
        address _curator,
        address _defaultDelegate,
        DAO_Creator _daoCreator,
        uint _proposalDeposit,
        uint _minTokensToCreate,
        uint _closingTime,
        address _privateCreation
    ) TokenCreation(_minTokensToCreate, _closingTime, _privateCreation) {

        curator = _curator;
        daoCreator = _daoCreator;
        proposalDeposit = _proposalDeposit;
        defaultDelegate = _defaultDelegate;
        rewardAccount = new ManagedAccount(address(this), false);
        DAOrewardAccount = new ManagedAccount(address(this), false);
        if (address(rewardAccount) == 0)
            throw;
        if (address(DAOrewardAccount) == 0)
            throw;
        lastTimeMinQuorumMet = now;
        minQuorumDivisor = 5; // sets the minimal quorum to 20%
        proposals.length = 1; // avoids a proposal with ID 0 because it is used

        allowedRecipients[address(this)] = true;
        allowedRecipients[curator] = true;

        VotesAssignedToDelegate[] assignations = votesAssignedToDelegates[defaultDelegate];
        assignations.length = 1;
        VotesAssignedToDelegate assignation = assignations[0];
        assignation.fromProposalId = 1;
    }

    function () returns (bool success) {
        if (now < closingTime + creationGracePeriod && msg.sender != address(extraBalance)) {
            uint oldTokens = balances[msg.sender];
            createTokenProxy(msg.sender);
            uint createdTokens = balances[msg.sender] - oldTokens;
            VotesAssignedToDelegate[] assignations = votesAssignedToDelegates[defaultDelegate];
            VotesAssignedToDelegate assignation = assignations[0];
            assignation.delegatedVotes += createdTokens;

            VotesAssignedToTokenHolder[] th_assignations = votesAssignedToTokenHolders[msg.sender];
            if (th_assignations.length == 0) {
                th_assignations.length++;
                VotesAssignedToTokenHolder th_assignation_new = th_assignations[0];
                th_assignation_new.fromProposalId = 1;
                th_assignation_new.votes = createdTokens;
            } else {
                VotesAssignedToTokenHolder th_assignation_actual = th_assignations[0];
                th_assignation_actual.votes += createdTokens;
            }
            return true;
        } else {
            return receiveEther();
        }
    }


    function receiveEther() returns (bool) {
        return true;
    }


    function newProposal(
        address _recipient,
        uint _amount,
        string _description,
        bytes _transactionData,
        uint _debatingPeriod,
        bool _newCurator
    ) onlyTokenholders returns (uint _proposalID) {

        // Sanity check
        if (_newCurator && (
            _amount != 0
            || _transactionData.length != 0
            || _recipient == curator
            || msg.value > 0
            || _debatingPeriod < minSplitDebatePeriod)) {
            throw;
        } else if (
            !_newCurator
            && (!isRecipientAllowed(_recipient) || (_debatingPeriod <  minProposalDebatePeriod))
        ) {
            throw;
        }

        if (_debatingPeriod > 8 weeks)
            throw;

        if (!isFueled
            || now < closingTime
            || (msg.value < proposalDeposit && !_newCurator)) {

            throw;
        }

        if (now + _debatingPeriod < now) // prevents overflow
            throw;

        // to prevent a 51% attacker to convert the ether into deposit
        if (msg.sender == address(this))
            throw;

        _proposalID = proposals.length++;
        Proposal p = proposals[_proposalID];
        p.recipient = _recipient;
        p.amount = _amount;
        p.description = _description;
        p.proposalHash = sha3(_recipient, _amount, _transactionData);
        p.votingDeadline = now + _debatingPeriod;
        p.open = true;
        //p.proposalPassed = False; // that's default
        p.newCurator = _newCurator;
        if (_newCurator)
            p.splitData.length++;
        p.creator = msg.sender;
        p.proposalDeposit = msg.value;

        sumOfProposalDeposits += msg.value;

        ProposalAdded(
            _proposalID,
            _recipient,
            _amount,
            _newCurator,
            _description
        );
    }


    function checkProposalCode(
        uint _proposalID,
        address _recipient,
        uint _amount,
        bytes _transactionData
    ) noEther constant returns (bool _codeChecksOut) {
        Proposal p = proposals[_proposalID];
        return p.proposalHash == sha3(_recipient, _amount, _transactionData);
    }


    function vote(
        uint _proposalID,
        bool _supportsProposal
    ) noEther returns (uint _voteID) {

        Proposal p = proposals[_proposalID];
        if (p.votedYes[msg.sender]
            || p.votedNo[msg.sender]
            || now >= p.votingDeadline) {

            throw;
        }

        var (votes, delegate) = getTokenHolderVotingRights(msg.sender, _proposalID);

        // Vote as a tokenHolder
        if (votes >0) {
            if (_supportsProposal) {
                p.yea += votes;
                p.votedYes[msg.sender] = true;
            } else {
                p.nay += votes;
                p.votedNo[msg.sender] = true;
            }

            p.bypassedDelegatedVotes[delegate] += votes;
            if (p.votedYes[delegate]) {
                p.yea -= votes;
            } else if (p.votedNo[delegate]) {
                p.nay -= votes;
            }

            if (blocked[msg.sender] == 0) {
                blocked[msg.sender] = _proposalID;
            } else if (p.votingDeadline > proposals[blocked[msg.sender]].votingDeadline) {
                // this proposal's voting deadline is further into the future than
                // the proposal that blocks the sender so make it the blocker
                blocked[msg.sender] = _proposalID;
            }
        }

        // Vote a delegate
        uint delegatedAssignedVotes = getDelegateVotingRights(msg.sender, _proposalID);
        if (delegatedAssignedVotes>0) {
            if (_supportsProposal) {
                p.yea += delegatedAssignedVotes - p.bypassedDelegatedVotes[msg.sender];
                p.votedYes[msg.sender] = true;
            } else {
                p.nay += delegatedAssignedVotes - p.bypassedDelegatedVotes[msg.sender];
                p.votedNo[msg.sender] = true;
            }
        }

        // Vote as a default delegate
        if (msg.sender == defaultDelegate) {
            delegatedAssignedVotes = getDelegateVotingRights(0, _proposalID);
            if (delegatedAssignedVotes>0) {
                if (_supportsProposal) {
                    p.yea += delegatedAssignedVotes - p.bypassedDelegatedVotes[0];
                    p.votedYes[msg.sender] = true;
                } else {
                    p.nay += delegatedAssignedVotes - p.bypassedDelegatedVotes[0];
                    p.votedNo[msg.sender] = true;
                }
            }
        }

        // Do not set the event if no voted
        if (   (! p.votedYes[msg.sender])
            && (! p.votedNo[msg.sender])) {
            throw;
        }

        Voted(_proposalID, _supportsProposal, msg.sender);
    }


    function executeProposal(
        uint _proposalID,
        bytes _transactionData
    ) noEther returns (bool _success) {

        Proposal p = proposals[_proposalID];

        uint waitPeriod = p.newCurator
            ? splitExecutionPeriod
            : executeProposalPeriod;
        // If we are over deadline and waiting period, assert proposal is closed
        if (p.open && now > p.votingDeadline + waitPeriod) {
            closeProposal(_proposalID);
            return;
        }

        // Check if the proposal can be executed
        if (now < p.votingDeadline  // has the voting deadline arrived?
            // Have the votes been counted?
            || !p.open
            // Does the transaction code match the proposal?
            || p.proposalHash != sha3(p.recipient, p.amount, _transactionData)) {

            throw;
        }

        // If the curator removed the recipient from the whitelist, close the proposal
        // in order to free the deposit and allow unblocking of voters
        if (!isRecipientAllowed(p.recipient) && p.open) {
            closeProposal(_proposalID);
            p.creator.send(p.proposalDeposit);
            return;
        }

        bool proposalCheck = true;

        if (p.amount > actualBalance())
            proposalCheck = false;

        uint quorum = p.yea + p.nay;

        // require 53% for calling newContract()
        if (_transactionData.length >= 4 && _transactionData[0] == 0x68
            && _transactionData[1] == 0x37 && _transactionData[2] == 0xff
            && _transactionData[3] == 0x1e
            && quorum < minQuorum(actualBalance() + rewardToken[address(this)])) {

                proposalCheck = false;
        }

        // Execute result
        if (quorum >= minQuorum(p.amount) && p.yea > p.nay && proposalCheck) {
            if (!p.creator.send(p.proposalDeposit))
                throw;

            lastTimeMinQuorumMet = now;

            if (!p.recipient.call.value(p.amount)(_transactionData))
                throw;

            p.proposalPassed = true;
            _success = true;
            rewardToken[address(this)] += p.amount;
            totalRewardToken += p.amount;
        } else if (quorum >= minQuorum(p.amount) && p.nay >= p.yea || !proposalCheck) {
            if (!p.creator.send(p.proposalDeposit))
                throw;
            lastTimeMinQuorumMet = now;
        }

        closeProposal(_proposalID);

        // Initiate event
        ProposalTallied(_proposalID, _success, quorum);
    }


    function closeProposal(uint _proposalID) internal {
        Proposal p = proposals[_proposalID];
        if (p.open)
            sumOfProposalDeposits -= p.proposalDeposit;
        p.open = false;
    }

    function splitDAO(
        uint _proposalID,
        address _newCurator
    ) noEther onlyTokenholders returns (bool _success) {

        Proposal p = proposals[_proposalID];

        // Sanity check

        if (now < p.votingDeadline  // has the voting deadline arrived?
            //The request for a split expires XX days after the voting deadline
            || now > p.votingDeadline + splitExecutionPeriod
            // Does the new Curator address match?
            || p.recipient != _newCurator
            // Is it a new curator proposal?
            || !p.newCurator
            // Have you voted for this split?
            || !p.votedYes[msg.sender]
            // Did you already vote on another proposal?
            || (blocked[msg.sender] != _proposalID && blocked[msg.sender] != 0) )  {

            throw;
        }

        // If the new DAO doesn't exist yet, create the new DAO and store the
        // current split data
        if (address(p.splitData[0].newDAO) == 0) {
            p.splitData[0].newDAO = createNewDAO(_newCurator);
            // Call depth limit reached, etc.
            if (address(p.splitData[0].newDAO) == 0)
                throw;
            // should never happen
            if (this.balance < sumOfProposalDeposits)
                throw;
            p.splitData[0].splitBalance = actualBalance();
            p.splitData[0].rewardToken = rewardToken[address(this)];
            p.splitData[0].totalSupply = totalSupply;
            p.proposalPassed = true;
        }

        // Move ether and assign new Tokens
        uint fundsToBeMoved =
            (balances[msg.sender] * p.splitData[0].splitBalance) /
            p.splitData[0].totalSupply;
        if (p.splitData[0].newDAO.createTokenProxy.value(fundsToBeMoved)(msg.sender) == false)
            throw;


        // Assign reward rights to new DAO
        uint rewardTokenToBeMoved =
            (balances[msg.sender] * p.splitData[0].rewardToken) /
            p.splitData[0].totalSupply;

        uint paidOutToBeMoved = DAOpaidOut[address(this)] * rewardTokenToBeMoved /
            rewardToken[address(this)];

        rewardToken[address(p.splitData[0].newDAO)] += rewardTokenToBeMoved;
        if (rewardToken[address(this)] < rewardTokenToBeMoved)
            throw;
        rewardToken[address(this)] -= rewardTokenToBeMoved;

        DAOpaidOut[address(p.splitData[0].newDAO)] += paidOutToBeMoved;
        if (DAOpaidOut[address(this)] < paidOutToBeMoved)
            throw;
        DAOpaidOut[address(this)] -= paidOutToBeMoved;

        // Burn DAO Tokens
        Transfer(msg.sender, 0, balances[msg.sender]);
        withdrawRewardFor(msg.sender); // be nice, and get his rewards
        totalSupply -= balances[msg.sender];
        balances[msg.sender] = 0;
        paidOut[msg.sender] = 0;
        return true;
    }

    function newContract(address _newContract){
        if (msg.sender != address(this) || !allowedRecipients[_newContract]) return;
        // move all ether
        if (!_newContract.call.value(address(this).balance)()) {
            throw;
        }

        //move all reward tokens
        rewardToken[_newContract] += rewardToken[address(this)];
        rewardToken[address(this)] = 0;
        DAOpaidOut[_newContract] += DAOpaidOut[address(this)];
        DAOpaidOut[address(this)] = 0;
    }

    function newDefaultDelegate(address _newDefaultDelegate){
        if (msg.sender != address(this) || !allowedRecipients[_newDefaultDelegate]) return;

        defaultDelegate = _newDefaultDelegate;

        NewDefaultDelegate(defaultDelegate);
    }


    function retrieveDAOReward(bool _toMembers) external noEther returns (bool _success) {
        DAO dao = DAO(msg.sender);

        if ((rewardToken[msg.sender] * DAOrewardAccount.accumulatedInput()) /
            totalRewardToken < DAOpaidOut[msg.sender])
            throw;

        uint reward =
            (rewardToken[msg.sender] * DAOrewardAccount.accumulatedInput()) /
            totalRewardToken - DAOpaidOut[msg.sender];
        if(_toMembers) {
            if (!DAOrewardAccount.payOut(dao.rewardAccount(), reward))
                throw;
            }
        else {
            if (!DAOrewardAccount.payOut(dao, reward))
                throw;
        }
        DAOpaidOut[msg.sender] += reward;
        return true;
    }

    function getMyReward() noEther returns (bool _success) {
        return withdrawRewardFor(msg.sender);
    }


    function withdrawRewardFor(address _account) noEther internal returns (bool _success) {
        if ((balanceOf(_account) * rewardAccount.accumulatedInput()) / totalSupply < paidOut[_account])
            throw;

        uint reward =
            (balanceOf(_account) * rewardAccount.accumulatedInput()) / totalSupply - paidOut[_account];
        if (!rewardAccount.payOut(_account, reward))
            throw;
        paidOut[_account] += reward;
        return true;
    }


    function transfer(address _to, uint256 _value) returns (bool success) {
        if (isFueled
            && now > closingTime
            && !isBlocked(msg.sender)
            && transferPaidOut(msg.sender, _to, _value)
            && super.transfer(_to, _value)) {

            var (votesFrom, delegateFrom) = getTokenHolderVotingRights(msg.sender, proposals.length+1);
            var (votesTo, delegateTo) = getTokenHolderVotingRights(_to, proposals.length+1);

            transferDelegateVotes(delegateFrom, delegateTo, _value);
            transferTokenHolderVotes(msg.sender, _to, _value);

            return true;
        } else {
            throw;
        }
    }


    function transferWithoutReward(address _to, uint256 _value) returns (bool success) {
        if (!getMyReward())
            throw;
        return transfer(_to, _value);
    }


    function transferFrom(address _from, address _to, uint256 _value) returns (bool success) {
        if (isFueled
            && now > closingTime
            && !isBlocked(_from)
            && transferPaidOut(_from, _to, _value)
            && super.transferFrom(_from, _to, _value)) {


            var (votesFrom, delegateFrom) = getTokenHolderVotingRights(_from, proposals.length+1);
            var (votesTo, delegateTo) = getTokenHolderVotingRights(_to, proposals.length+1);

            transferDelegateVotes(delegateFrom, delegateTo, _value);
            transferTokenHolderVotes(_from, _to, _value);


            return true;
        } else {
            throw;
        }
    }


    function transferFromWithoutReward(
        address _from,
        address _to,
        uint256 _value
    ) returns (bool success) {

        if (!withdrawRewardFor(_from))
            throw;
        return transferFrom(_from, _to, _value);
    }


    function transferPaidOut(
        address _from,
        address _to,
        uint256 _value
    ) internal returns (bool success) {

        uint transferPaidOut = paidOut[_from] * _value / balanceOf(_from);
        if (transferPaidOut > paidOut[_from])
            throw;
        paidOut[_from] -= transferPaidOut;
        paidOut[_to] += transferPaidOut;
        return true;
    }


    function changeProposalDeposit(uint _proposalDeposit) noEther external {
        if (msg.sender != address(this) || _proposalDeposit > (actualBalance() + rewardToken[address(this)])
            / maxDepositDivisor) {

            throw;
        }
        proposalDeposit = _proposalDeposit;
    }


    function changeAllowedRecipients(address _recipient, bool _allowed) noEther external returns (bool _success) {
        if (msg.sender != curator)
            throw;
        allowedRecipients[_recipient] = _allowed;
        AllowedRecipientChanged(_recipient, _allowed);
        return true;
    }


    function isRecipientAllowed(address _recipient) internal returns (bool _isAllowed) {
        if (allowedRecipients[_recipient]
            || (_recipient == address(extraBalance)
                // only allowed when at least the amount held in the
                // extraBalance account has been spent from the DAO
                && totalRewardToken > extraBalance.accumulatedInput()))
            return true;
        else
            return false;
    }

    function actualBalance() constant returns (uint _actualBalance) {
        return this.balance - sumOfProposalDeposits;
    }


    function minQuorum(uint _value) internal constant returns (uint _minQuorum) {
        // minimum of 20% and maximum of 53.33%
        return totalSupply / minQuorumDivisor +
            (_value * totalSupply) / (3 * (actualBalance() + rewardToken[address(this)]));
    }


    function halveMinQuorum() returns (bool _success) {
        if (lastTimeMinQuorumMet < (now - quorumHalvingPeriod)) {
            lastTimeMinQuorumMet = now;
            minQuorumDivisor *= 2;
            return true;
        } else {
            return false;
        }
    }

    function createNewDAO(address _newCurator) internal returns (DAO _newDAO) {
        NewCurator(_newCurator);
        return daoCreator.createDAO(_newCurator, _newCurator, 0, 0, now + splitExecutionPeriod);
    }


    function numberOfProposals() constant returns (uint _numberOfProposals) {
        // Don't count index 0. It's used by isBlocked() and exists from start
        return proposals.length - 1;
    }

    function getNewDAOAdress(uint _proposalID) constant returns (address _newDAO) {
        return proposals[_proposalID].splitData[0].newDAO;
    }


    function isBlocked(address _account) returns (bool) {
        if (blocked[_account] == 0)
            return false;
        Proposal p = proposals[blocked[_account]];
        if (now > p.votingDeadline) {
            blocked[_account] = 0;
            return false;
        } else {
            return true;
        }
    }

    function setDelegate(address _delegate) onlyTokenholders noEther returns (bool) {

            var (votes, oldDelegate) = getTokenHolderVotingRights(msg.sender, proposals.length+1);

            if (oldDelegate == _delegate) {
                throw;
            }
            transferDelegateVotes(oldDelegate, _delegate, votes);

            VotesAssignedToTokenHolder[] assignements = votesAssignedToTokenHolders[msg.sender];

            if ((assignements.length == 0) || (assignements[assignements.length -1].fromProposalId <= proposals.length)) {
                VotesAssignedToTokenHolder newAssignment = assignements[ assignements.length++ ];
                newAssignment.fromProposalId = proposals.length+1;
                newAssignment.delegate = _delegate;
                newAssignment.votes = votes;
            } else {
                VotesAssignedToTokenHolder oldAssignment  = assignements[assignements.length-1];
                oldAssignment.delegate = _delegate;
            }

            return true;
    }


    function getTokenHolderVotingRights(address _tokenHolder, uint _proposalID) constant returns (uint votes, address delegate) {
            VotesAssignedToTokenHolder[] assignements = votesAssignedToTokenHolders[_tokenHolder];
            if (assignements.length == 0) {
                return (0,0);
            }

            uint i;

            for (i = assignements.length; i>0; i-- ) {
                VotesAssignedToTokenHolder a = assignements[i-1];
                if (_proposalID >= a.fromProposalId) return (a.votes, a.delegate);
            }

            return (0,0);
    }


    function getDelegateVotingRights(address _delegate, uint _proposalID) constant returns (uint votes) {
            VotesAssignedToDelegate[] assignements = votesAssignedToDelegates[_delegate];
            if (assignements.length == 0) return 0;

            uint i;

            for (i = assignements.length; i>0; i-- ) {
                VotesAssignedToDelegate a = assignements[i-1];
                if (_proposalID >= a.fromProposalId) return a.delegatedVotes;
            }

            return 0;
    }

    function transferDelegateVotes(address _delegateFrom, address _delegateTo, uint _value) internal constant returns(bool) {

            if (_value == 0) {
                return true;
            }

// Remove _delegateFrom votes
            uint previousVotesFrom = getDelegateVotingRights(_delegateFrom, proposals.length+1);
            if (previousVotesFrom < _value) {
                throw;
            }

            VotesAssignedToDelegate[] assignmentsFrom = votesAssignedToDelegates[_delegateFrom];

            if ((assignmentsFrom.length == 0) || (assignmentsFrom[assignmentsFrom.length -1].fromProposalId <= proposals.length)) {
                VotesAssignedToDelegate newAssignmentFrom = assignmentsFrom[ assignmentsFrom.length++ ];
                newAssignmentFrom.fromProposalId = proposals.length+1;
                newAssignmentFrom.delegatedVotes = previousVotesFrom - _value;
            } else {
                VotesAssignedToDelegate oldAssignmentFrom = assignmentsFrom[assignmentsFrom.length-1];
                oldAssignmentFrom.delegatedVotes = previousVotesFrom - _value;
            }

// Add _delegateTo votes
            uint previousVotesTo = getDelegateVotingRights(_delegateTo, proposals.length+1);

            VotesAssignedToDelegate[] assignmentsTo = votesAssignedToDelegates[_delegateTo];

            if ((assignmentsTo.length == 0) || (assignmentsTo[assignmentsTo.length -1].fromProposalId <= proposals.length)) {
                VotesAssignedToDelegate newAssignmentTo = assignmentsTo[ assignmentsTo.length++ ];
                newAssignmentTo.fromProposalId = proposals.length+1;
                newAssignmentTo.delegatedVotes = previousVotesTo + _value;
            } else {
                var oldAssignmentTo = assignmentsTo[assignmentsTo.length-1];
                oldAssignmentTo.delegatedVotes = previousVotesTo + _value;
            }

            return true;
    }

    function transferTokenHolderVotes(address _from, address _to, uint _value) internal constant returns(bool) {

        if (_value == 0) {
            return true;
        }

// Remove _from votes
        var (previousVotesFrom, previousDelegateFrom) = getTokenHolderVotingRights(_from, proposals.length+1);
        if (previousVotesFrom < _value) {
            throw;
        }

        VotesAssignedToTokenHolder[] assignmentsFrom = votesAssignedToTokenHolders[_from];

        if ((assignmentsFrom.length == 0) || (assignmentsFrom[assignmentsFrom.length -1].fromProposalId <= proposals.length)) {
            VotesAssignedToTokenHolder newAssignmentFrom = assignmentsFrom[ assignmentsFrom.length++ ];
            newAssignmentFrom.fromProposalId = proposals.length+1;
            newAssignmentFrom.votes = previousVotesFrom - _value;
            newAssignmentFrom.delegate = previousDelegateFrom;
        } else {
            VotesAssignedToTokenHolder oldAssignmentFrom = assignmentsFrom[assignmentsFrom.length-1];
            oldAssignmentFrom.votes = previousVotesFrom - _value;
        }

// Add _delegateTo votes
        var (previousVotesTo, previousDelegateTo) = getTokenHolderVotingRights(_to, proposals.length+1);

        VotesAssignedToTokenHolder[] assignmentsTo = votesAssignedToTokenHolders[_to];

        if ((assignmentsTo.length == 0) || (assignmentsTo[assignmentsTo.length -1].fromProposalId <= proposals.length)) {
            VotesAssignedToTokenHolder newAssignmentTo = assignmentsTo[ assignmentsTo.length++ ];
            newAssignmentTo.fromProposalId = proposals.length+1;
            newAssignmentTo.votes = previousVotesTo + _value;
            newAssignmentTo.delegate = previousDelegateTo;
        } else {
            VotesAssignedToTokenHolder oldAssignmentTo = assignmentsTo[assignmentsTo.length-1];
            oldAssignmentTo.votes = previousVotesTo + _value;
        }

        return true;
    }

}

contract DAO_Creator {
    function createDAO(
        address _curator,
        address _defaultDelegate,
        uint _proposalDeposit,
        uint _minTokensToCreate,
        uint _closingTime
    ) returns (DAO _newDAO) {

        return new DAO(
            _curator,
            _defaultDelegate,
            DAO_Creator(this),
            _proposalDeposit,
            _minTokensToCreate,
            _closingTime,
            msg.sender
        );
    }
}
